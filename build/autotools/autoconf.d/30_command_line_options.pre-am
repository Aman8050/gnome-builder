AC_ARG_ENABLE([tracing],
              [AS_HELP_STRING([--enable-tracing=@<:@no/yes@:>@],
                              [turn on tracing @<:@default=no@:>@])],
                              [],
                              [enable_tracing=no])

# use strict compiler flags only on development releases
m4_define([maintainer_flags_default], [m4_if(m4_eval(project_minor_version % 2), [1], [yes], [no])])
AC_ARG_ENABLE([maintainer-flags],
              [AS_HELP_STRING([--enable-maintainer-flags=@<:@no/yes@:>@],
                              [Use strict compiler flags @<:@default=]maintainer_flags_default[@:>@])],
              [],
              [enable_maintainer_flags=maintainer_flags_default])

dnl **************************************************************************
dnl Enable extra debugging options
dnl **************************************************************************
m4_define([debug_default],
	  [m4_if(m4_eval(project_minor_version % 2), [1],
	  [yes], [minimum])])
AC_ARG_ENABLE([debug],
	      [AS_HELP_STRING([--enable-debug=@<:@no/minimum/yes@:>@],
	      		      [turn on debugging @<:@default=debug_default@:>@])],
	      		      [],
	      		      [enable_debug=debug_default])
AS_CASE([$enable_debug],
	[yes], [],
	[minimum], [
		CFLAGS="$CFLAGS -DG_DISABLE_CAST_CHECKS"
		CFLAGS="$CFLAGS -DIDE_DISABLE_DEBUG"
		CFLAGS="$CFLAGS -DIDE_DISABLE_TRACE"
	],
	[no], [
		CFLAGS="$CFLAGS -DG_DISABLE_ASSERT"
		CFLAGS="$CFLAGS -DG_DISABLE_CHECKS"
		CFLAGS="$CFLAGS -DG_DISABLE_CAST_CHECKS"
		CFLAGS="$CFLAGS -DIDE_DISABLE_DEBUG"
		CFLAGS="$CFLAGS -DIDE_DISABLE_TRACE"
	],
	[AC_MSG_ERROR([Unknown argument to --enable-debug])]
)
